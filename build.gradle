plugins {
    id 'java'
}

group = 'ru.fennec.free'
version = '0.1-БЕТА'
ext.projectVersion = 'b1'

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

def createPropertiesIfNotExist() {
    def versionPropsFile = file('version.properties')
    if(!versionPropsFile.exists()) {
        versionPropsFile.createNewFile()
        BufferedWriter writer = new BufferedWriter(new FileWriter('version.properties'))
        writer.write("build=0")
        writer.close()
    }

}

def getBuildVersion() {
    createPropertiesIfNotExist()
    def versionPropsFile = file('version.properties')
    if(versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        return versionProps['build'].toInteger()
    } else {
        throw new GradleException("Could no read properties")
    }
}

def setBuildVersion(buildVersion) {
    createPropertiesIfNotExist()
    def versionPropsFile = file('version.properties')
    if(versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        versionProps['build'] = buildVersion.toString()
        versionProps.store(versionPropsFile.newWriter(), null)
    } else {
        throw new GradleException("Could no read properties")
    }
}

def incrementBuildVersion() {
    def buildOutputFolder = file('build/libs')
    buildOutputFolder.mkdirs()
    Arrays.asList(buildOutputFolder.listFiles()).forEach(file -> file.delete())

    def code = getBuildVersion() + 1
    setBuildVersion(code)
    version projectVersion + "-build" + code
}

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        url = uri("https://maven.enginehub.org/repo/")
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT'
    implementation 'space.arim.dazzleconf:dazzleconf-ext-snakeyaml:1.3.0-M2'
    implementation group: 'org.jdbi', name: 'jdbi3-core', version: '3.29.0'
    implementation group: 'org.jdbi', name: 'jdbi3-sqlite', version: '3.34.0'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
    compileOnly 'me.clip:placeholderapi:2.11.2'
    compileOnly 'com.sk89q.worldedit:worldedit-core:7.2.9'
    compileOnly 'com.sk89q.worldedit:worldedit-bukkit:7.2.9'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

jar {
    incrementBuildVersion()
//    dependsOn configurations.runtimeClasspath
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.findAll {it.name.endsWith('jar') }.collect { zipTree(it) }
//        setDuplicatesStrategy(DuplicatesStrategy.INCLUDE)
    }
}
